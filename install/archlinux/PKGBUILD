# Maintainer: JordanViknar

pkgname=simplesteamtinker-git
pkgver=r33.eab556b
pkgrel=1
pkgdesc="A work-in-progress simple, fast and modern Adwaita alternative to SteamTinkerLaunch."
arch=('x86_64' 'i686' 'armv7h' 'aarch64')
url=https://github.com/JordanViknar/SimpleSteamTinker
license=('MPL2')
makedepends=(
	'git'
)
depends=(
	'lua'
	'lua-lgi-git' # Used for GTK (git version because latest release neither supports Libadwaita nor GTK4 and is pretty old)
	'libadwaita'
	'gtk4'
	'lua-filesystem' # Used for some filesystem operations
	'lua-socket' # Used for network operations
	'lua-sec' # Used for secure https interactions
	'lua-dkjson' # Used for json parsing
	'xclip' # Used for copying to clipboard
	'libnotify' # Used for notifications
)
optdepends=(
	'gamemode: Enable GameMode support'
	'mangohud: Enable MangoHud support'
	'mesa: Enable Zink support'
	'switcheroo-control: Launch games with your dedicated GPU'
	'gamescope: Enable GameScope support'
	'protonplus: Recommended to install with SimpleSteamTinker'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/JordanViknar/SimpleSteamTinker')
sha256sums=('SKIP')

pkgver() {
	cd SimpleSteamTinker
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	cd SimpleSteamTinker
	install -Dm755 sst -t "${pkgdir}/usr/bin/"
 	install -Dm644 assets/desktop/system.desktop \
  		"${pkgdir}/usr/share/applications/SimpleSteamTinker.desktop"
	install -Dm644 assets/icons/256x256/${pkgname%-git}.png -t \
 		"${pkgdir}/usr/share/icons/hicolor/256x256/apps/"
	install -Dm644 assets/icons/scalable/${pkgname%-git}.svg -t \
		"${pkgdir}/usr/share/icons/hicolor/scalable/apps/"

	install -Dm644 main.lua -t "${pkgdir}/usr/share/SimpleSteamTinker/"
	cp -r modules "${pkgdir}/usr/share/SimpleSteamTinker/modules"
	install -Dm644 ui/main.ui -t "${pkgdir}/usr/share/SimpleSteamTinker/ui/"
}
